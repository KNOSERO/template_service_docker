pipeline {
    agent {
        label 'home'
    }
    options {
        skipDefaultCheckout();
        timestamps();
        ansiColor('xterm')
    }
    environment {
        REPO_URL = 'https://github.com/KNOSERO/docker_services.git'
        CREDITS_ID = 'github-token'
        ACCOUNT = 'KNOSERO'
        REPO = 'docker_services'
    }

    stages {
        stage('Checkout') {
            steps {
                checkout([$class: 'GitSCM',
                    userRemoteConfigs: [[
                        name: 'origin',
                        url: env.REPO_URL,
                        credentialsId: env.CREDITS_ID,
                        refspec: '+refs/heads/*:refs/remotes/origin/*'
                    ]],
                    branches: [[ name: "${env.BRANCH}" ]],
                    extensions: [
                        [$class: 'CloneOption', shallow: true, depth: 25, noTags: true, timeout: 240],
                        [$class: 'PruneStaleBranch'],
                        [$class: 'CleanBeforeCheckout']
                    ]
                ])
            }
        }

        stage('Init') {
            parallel {
                stage('SSH key configuration') {
                    steps {
                        withCredentials([sshUserPrivateKey(credentialsId: 'id_home_lab', keyFileVariable: 'SSH_KEY')]) {
                            sh '''
                                mkdir -p /root/.ssh
                                cp "$SSH_KEY" /root/.ssh/id_home_lab
                                chmod 600 /root/.ssh/id_home_lab
                               '''
                        }
                    }
                }

                stage('Init Submodules') {
                    steps {
                        script {
                            sh '''
                                git config --global url."https://github.com/".insteadOf "git@github.com:"
                                git submodule sync --recursive
                                git submodule update --init --recursive --remote
                            '''
                        }
                    }
                }
            }
        }

        stage('Prepare Config') {
            steps {
                script {
                    def credsMap = [
                        vpn: 'secret-file-vpn'
                    ]

                    def credentialsId = credsMap[params.SERVICE]

                    if (credentialsId) {
                        echo "Używam sekretnych plików: ${credentialsId}"
                        withCredentials([file(credentialsId: credentialsId, variable: 'SECRET_FILE')]) {
                            sh """
                                mkdir -p ${params.SERVICE}
                                cp "\$SECRET_FILE" "${params.SERVICE}/.env"
                            """
                        }
                    } else {
                        echo "Brak skonfigurowanego sekretu dla ${params.SERVICE}, pomijam"
                    }

                    writeFile file: 'inventory.ini', text: params.CONFIG
                }
            }
        }

        stage('Run playbook Ansible') {
            environment {
                ANSIBLE_HOST_KEY_CHECKING = 'False'
            }
            steps {
                script {
                    def playbookPath = "playbook.yml"
                    sh "ansible-galaxy install -r ansible/requirements.yml"
                    sh "ansible-playbook -i inventory.ini ${playbookPath}"
                }
            }
        }
    }
}